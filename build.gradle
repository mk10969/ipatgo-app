plugins {
    id 'org.springframework.boot' version '2.2.4.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    group 'org.uma.external'
    version '1.0.0.RELEASE'

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    configurations {
        testImplementation.extendsFrom compileOnly
        testAnnotationProcessor.extendsFrom annotationProcessor
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }

    dependencies {
        // lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        // use JUnit 5
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

    test {
        useJUnitPlatform()
    }

    compileJava.options.encoding = 'UTF-8'
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

}

